apply plugin: 'com.android.application'

android {
    signingConfigs {
        def debugKeystore = new File("${project.rootDir}/debug.keystore")
        apply from: debugKeystore, to: android
    }
    compileSdkVersion 26
    // flavors properties were not used as combined one before, but this allows combinations of flavors.
    flavorDimensions "applicationId", "minSdkVersion"
    defaultConfig {
        applicationId "com.example.nankiti.android_gradle3"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
    }
    variantFilter { variant ->
        // If you have any outputs you don't need, just set ignore.
        if (variant.getFlavors().get(0).name == 'minApi18' && variant.buildType.name == "release") {
            variant.setIgnore(true);
        }
    }
    buildTypes {
        release {
            // This is only used for production.
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // development environment
            versionNameSuffix "-debug"
            signingConfig signingConfigs.config
            applicationIdSuffix ".debug"
        }
        staging {
            // for integration such as distribution to QA.
            versionNameSuffix "-staging"
            applicationIdSuffix ".staging"
            // When gradle couldn't find out the matching dependencies in any other modules.
            // We can specify which one to pick up for each dependencies.
            matchingFallbacks = ['debug']
        }
    }
    productFlavors {
        minApi18 {
            dimension "minSdkVersion"
            minSdkVersion 24
        }
        minApi15 {
            dimension "minSdkVersion"
            minSdkVersion 15
        }
        us {
            dimension "applicationId"
            applicationId "com.bison.sampleapp"
            // matchingFallbacks = ['the flavor type that other modules have']
        }
        japan {
            dimension "applicationId"
            applicationId "jp.co.bison.sampleapp"
            // matchingFallbacks = ['the flavor type that other modules have']
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    compile project(':bison')
}
